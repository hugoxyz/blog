# 如何手动在eclipse原工程上把其转化为android studio工程

## 原由
android studio可以把eclipse工程import为gradle工程的，但是它import会，会拷贝一份eclipse工程，重新创建一个新工程，而我因为工程需要, eclipse工程也需要保留。这样的话，如果想用 AS 来改动代码，每次都要把代码的改动同步到 eclipse 工程中去，很麻烦，所以我想在 eclipse 原工程中，直接让它支持 android studio 工程。可以达到 eclipse 同时支技 eclipse 和 android studio, 研究了下 android studio 的工程设置后。用以下步骤可以做到.

## 简单说明
android studio 的工程与eclipse工程的一个不同之处在于，AS就有一个主工程设置，eclipse中的应用工程，子工程在android studio中都是一个module,至于这个module是要编成库，还是编成应用，就看这个module对应的build.gradle文件里怎么写了，

这是一个库
```gradle
apply plugin: 'com.android.library'
```

这是一个应用
```gradle
apply plugin: 'com.android.application'
```
每一个module中，只要加一个build.gradle文件，AS就能识别到,build.gradle就是一个groovy语法文件,这也只是一个入口文件，如果有需要，也可能会把build.gradle 分成好几个文件，这里只有一个build.gradle

android studio的工程设置，包含了一些gradle的全局设置，sdk,ndk路径，当前的android studio工程所包含的module,它们的路径在哪里。

## 开始

现在我有一个这样的eclipse工程

App    (应用工程)
libsdk  (库工程)

1.在 App 工程中放入包含以下内容的build.gradle文件

build.gradle
```
apply plugin: 'com.android.application'    //表明这个module是一个应用工程

android {
    compileSdkVersion 23		//编译sdk版本
    buildToolsVersion "23.0.2"  //编译工具版本

    defaultConfig {
        applicationId "org.hugo.hugotest" //工程的包名
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    //这里要特别注意,这里将eclipse工程的目录与AS工程的目录作了映射
    sourceSets.main {
        aidl.srcDir "src"   //如果你的src中有aidl文件，那么这个映射是需要的，如果没有，可以删除
        java.srcDir "src"
        res.srcDir "res"
        jniLibs.srcDir "libs"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "assets"
    }
}

//这里写明了App这个工程所依赖的库
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':libsdk')
}
```
一般情况，把这个文件直接拷到App下就可以用了，当然，如果你要设置 build 时的混淆，也是在这个文件里设置。

2.在 libsdk 工程中放入包含以下内容的 build.gradle 文件

build.gradle,这个和App中的除了第一句，其余基本一样
```gradle
apply plugin: 'com.android.library' //表时这是一个库工程

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        jniLibs.srcDir "libs"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "assets"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.google.android.gms:play-services:8.4.0'
}
```

3.创建一个目录，目录下放以下内容

build.gradle

gradle.properties //没有特别设置的话，为空

gradle/wrapper/gradle-wrapper.jar           //wrapper，用as创建个工程，拷过来吧，

gradle/wrapper/gradle-wrapper.properties    //wrapper，用as创建个工程，拷过来吧，

gradlew 									//wrapper，用as创建个工程，拷过来吧，

gradlew.bat 								//wrapper，用as创建个工程，拷过来吧，

settings.gradle

文件内容:

build.gradle
```gradle
// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

```

settings.gradle
这个文件包含了as工程中所有module
```gradle
include ':app'
project(':app').projectDir = new File(settingsDir, '../proj.android')

include ':libsdk'
project(':libsdk').projectDir = new File(settingsDir, '../libsdk/proj.android')

```

